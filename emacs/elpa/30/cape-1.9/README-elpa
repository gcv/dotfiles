                 â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”
                  CAPE.EL - LET YOUR COMPLETIONS FLY!
                 â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”


Cape provides Completion At Point Extensions which can be used in
combination with [Corfu], [Company] or the default completion UI. The
completion backends used by `completion-at-point' are so called
`completion-at-point-functions' (Capfs).

You can register the `cape-*' functions in the
`completion-at-point-functions' list.  This makes the backends available
for completion, which is usually invoked by pressing `TAB' or
`M-TAB'. The functions can also be invoked interactively to trigger the
respective completion at point. You can bind them directly to a key in
your user configuration. Notable commands/Capfs are `cape-line' for
completion of a line from the current buffer, `cape-history' for history
completion in shell or Comint modes and `cape-file' for completion of
file names. The commands `cape-elisp-symbol' and `cape-elisp-block' are
useful for documentation of Elisp packages or configurations, since they
complete Elisp anywhere.

Cape has the super power to transform Company backends into Capfs and
merge multiple Capfs into a Super-Capf! These transformers allow you to
still take advantage of Company backends even if you are not using
Company as frontend.

Table of Contents
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

1. Available Capfs
2. Configuration
3. CAPF adapters and transformers
.. 1. Company adapter
.. 2. Super-Capf - Merging multiple Capfs
.. 3. Capf-Buster - Cache busting
.. 4. Capf transformers
4. Contributions


[Corfu] <https://github.com/minad/corfu>

[Company] <https://github.com/company-mode/company-mode>


1 Available Capfs
â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•

  âƒ `cape-abbrev': Complete abbreviation (`add-global-abbrev',
    `add-mode-abbrev').
  âƒ `cape-dabbrev': Complete word from current buffers. See also
    `dabbrev-capf' on Emacs 29.
  âƒ `cape-dict': Complete word from dictionary file.
  âƒ `cape-elisp-block': Complete Elisp in Org or Markdown code block.
  âƒ `cape-elisp-symbol': Complete Elisp symbol.
  âƒ `cape-emoji': Complete Emoji. Available on Emacs 29 and newer.
  âƒ `cape-file': Complete file name.
  âƒ `cape-history': Complete from Eshell, Comint or minibuffer history.
  âƒ `cape-keyword': Complete programming language keyword.
  âƒ `cape-line': Complete entire line from current buffer.
  âƒ `cape-rfc1345': Complete Unicode char using RFC 1345 mnemonics.
  âƒ `cape-sgml': Complete Unicode char from SGML entity, e.g., `&alpha'.
  âƒ `cape-tex': Complete Unicode char from TeX command, e.g. `\hbar'.


2 Configuration
â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•

  Cape is available on GNU ELPA and MELPA. You can install the package
  with `package-install'. In the following we present a sample
  configuration based on the popular `use-package' macro.

  I recommend to bind the `cape-*' completion commands to keys such that
  you can invoke them explicitly. This makes particular sense for
  special Capfs which you only want to trigger in rare
  circumstances. See the `:bind' specification below.

  Furthermore the `cape-*' functions are Capfs which you can add to the
  `completion-at-point-functions' list. Take care when adding Capfs to
  the list since each of the Capfs adds a small runtime cost. Note that
  the Capfs which occur earlier in the list take precedence, such that
  the first Capf returning a result will win and the later Capfs may not
  get a chance to run. In order to merge Capfs you can try the function
  `cape-capf-super'.

  One must distinguish the buffer-local and the global value of the
  `completion-at-point-functions' variable. The buffer-local value of
  the list takes precedence, but if the buffer-local list contains the
  symbol `t' at the end, it means that the functions specified in the
  global list should be executed afterwards. The special meaning of the
  value `t' is a feature of the `run-hooks' function, see the section
  ["Running Hooks" in the Elisp manual] for further information.

  â”Œâ”€â”€â”€â”€
  â”‚ ;; Enable Corfu completion UI
  â”‚ ;; See the Corfu README for more configuration tips.
  â”‚ (use-package corfu
  â”‚   :init
  â”‚   (global-corfu-mode))
  â”‚ 
  â”‚ ;; Add extensions
  â”‚ (use-package cape
  â”‚   ;; Bind prefix keymap providing all Cape commands under a mnemonic key.
  â”‚   ;; Press C-c p ? to for help.
  â”‚   :bind ("C-c p" . cape-prefix-map) ;; Alternative key: M-<tab>, M-p, M-+
  â”‚   ;; Alternatively bind Cape commands individually.
  â”‚   ;; :bind (("C-c p d" . cape-dabbrev)
  â”‚   ;;        ("C-c p h" . cape-history)
  â”‚   ;;        ("C-c p f" . cape-file)
  â”‚   ;;        ...)
  â”‚   :init
  â”‚   ;; Add to the global default value of `completion-at-point-functions' which is
  â”‚   ;; used by `completion-at-point'.  The order of the functions matters, the
  â”‚   ;; first function returning a result wins.  Note that the list of buffer-local
  â”‚   ;; completion functions takes precedence over the global list.
  â”‚   (add-hook 'completion-at-point-functions #'cape-dabbrev)
  â”‚   (add-hook 'completion-at-point-functions #'cape-file)
  â”‚   (add-hook 'completion-at-point-functions #'cape-elisp-block)
  â”‚   ;; (add-hook 'completion-at-point-functions #'cape-history)
  â”‚   ;; ...
  â”‚ )
  â””â”€â”€â”€â”€


["Running Hooks" in the Elisp manual] <info:elisp#Running Hooks>


3 CAPF adapters and transformers
â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•

3.1 Company adapter
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

  /Wrap your Company backend in a Cape and turn it into a Capf!/

  Cape provides the adapter `cape-company-to-capf' for Company
  backends. The adapter transforms Company backends to Capfs which are
  understood by the built-in Emacs completion mechanism. The function is
  approximately the inverse of the `company-capf' backend from
  Company. The adapter can be used as follows:

  â”Œâ”€â”€â”€â”€
  â”‚ ;; Use Company backends as Capfs.
  â”‚ (setq-local completion-at-point-functions
  â”‚   (mapcar #'cape-company-to-capf
  â”‚     (list #'company-files #'company-keywords #'company-dabbrev)))
  â””â”€â”€â”€â”€

  Note that the adapter does not require Company to be installed or
  enabled.  Backends implementing the Company specification do not
  necessarily have to depend on Company, however in practice most
  backends do. The following shows a small example completion backend,
  which can be used with both `completion-at-point' (Corfu, default
  completion) and Company.

  â”Œâ”€â”€â”€â”€
  â”‚ (defvar demo-alist
  â”‚   '((":-D" . "ğŸ˜€")
  â”‚     (";-)" . "ğŸ˜‰")
  â”‚     (":-/" . "ğŸ˜•")
  â”‚     (":-(" . "ğŸ™")
  â”‚     (":-*" . "ğŸ˜™")))
  â”‚ 
  â”‚ (defun demo-backend (action &optional arg &rest _)
  â”‚   (pcase action
  â”‚     ('prefix (and (memq (char-before) '(?: ?\;))
  â”‚ 		  (cons (string (char-before)) t)))
  â”‚     ('candidates (all-completions arg demo-alist))
  â”‚     ('annotation (concat " " (cdr (assoc arg demo-alist))))
  â”‚     ('post-completion
  â”‚      (let ((str (buffer-substring (- (point) 3) (point))))
  â”‚        (delete-region (- (point) 3) (point))
  â”‚      (insert (cdr (assoc str demo-alist)))))))
  â”‚ 
  â”‚ ;; Register demo backend with `completion-at-point'
  â”‚ (setq completion-at-point-functions
  â”‚       (list (cape-company-to-capf #'demo-backend)))
  â”‚ 
  â”‚ ;; Register demo backend with Company.
  â”‚ (setq company-backends '(demo-backend))
  â””â”€â”€â”€â”€

  It is possible to merge multiple Company backends and use them as a
  single Capf using the `company--multi-backend-adapter' function from
  Company. The adapter transforms multiple Company backends into a
  single Company backend, which can then be used as a Capf via
  `cape-company-to-capf'. Capfs can be merged directly with
  `cape-capf-super'.

  â”Œâ”€â”€â”€â”€
  â”‚ (require 'company)
  â”‚ ;; Use the company-dabbrev and company-elisp backends together.
  â”‚ (setq completion-at-point-functions
  â”‚       (list
  â”‚        (cape-company-to-capf
  â”‚ 	(apply-partially #'company--multi-backend-adapter
  â”‚ 			 '(company-dabbrev company-elisp)))))
  â””â”€â”€â”€â”€


3.2 Super-Capf - Merging multiple Capfs
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

  /Throw multiple Capfs under the Cape and get a Super-Capf!/

  Cape supports merging multiple Capfs using the function
  `cape-capf-super'. Due to some technical details, not all Capfs can be
  merged successfully. Merge Capfs one by one and make sure that you get
  the desired outcome.

  Note that `cape-capf-super' is not needed if multiple Capfs should
  betried one after the other, for example you can use `cape-file'
  together with programming mode Capfs by adding `cape-file' to the
  `completion-at-point-functions' list. File completion will then be
  available in comments and string literals, but not in normal
  code. `cape-capf-super' is only necessary if you want to combine
  multiple Capfs, such that the candidates from multiple sources appear
  /together/ in the completion list at the same time.

  Capf merging requires completion functions which are sufficiently
  well-behaved and completion functions which do not define completion
  boundaries.  `cape-capf-super' has the same restrictions as
  `completion-table-merge' and `completion-table-in-turn'. As a simple
  rule of thumb, `cape-capf-super' works for static completion functions
  like `cape-dabbrev', `cape-keyword', `cape-dict', etc., but not for
  multi-step completions like `cape-file'.

  â”Œâ”€â”€â”€â”€
  â”‚ ;; Merge the dabbrev, dict and keyword capfs, display candidates together.
  â”‚ (setq-local completion-at-point-functions
  â”‚ 	    (list (cape-capf-super #'cape-dabbrev #'cape-dict #'cape-keyword)))
  â”‚ 
  â”‚ ;; Alternative: Define named Capf instead of using the anonymous Capf directly
  â”‚ (defun cape-dabbrev-dict-keyword ()
  â”‚   (cape-wrap-super #'cape-dabbrev #'cape-dict #'cape-keyword))
  â”‚ (setq-local completion-at-point-functions (list #'cape-dabbrev-dict-keyword))
  â””â”€â”€â”€â”€

  See also the aforementioned `company--multi-backend-adapter' from
  Company, which allows you to merge multiple Company backends.


3.3 Capf-Buster - Cache busting
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

  /The Capf-Buster ensures that you always get a fresh set of
  candidates!/

  If a Capf caches the candidates for too long we can use a cache
  busting Capf-transformer. For example the Capf merging function
  `cape-capf-super' creates a Capf, which caches the candidates for the
  whole lifetime of the Capf.  Therefore you may want to combine a
  merged Capf with a cache buster under some circumstances. It is
  noteworthy that the `company-capf' backend from Company refreshes the
  completion table frequently. With the `cape-capf-buster' we can
  achieve a similarly refreshing strategy.

  â”Œâ”€â”€â”€â”€
  â”‚ (setq-local completion-at-point-functions
  â”‚ 	    (list (cape-capf-buster #'some-caching-capf)))
  â””â”€â”€â”€â”€


3.4 Capf transformers
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

  Cape provides a set of additional Capf transformation functions, which
  are mostly meant to used by experts to fine tune the Capf behavior and
  Capf interaction. These can either be used as advices (`cape-wrap-*)'
  or to create a new Capf from an existing Capf (`cape-capf-*'). You can
  bind the Capfs created by the Capf transformers with `defalias' to a
  function symbol.

  â€¢ `cape-capf-accept-all', `cape-wrap-accept-all': Create a Capf which
    accepts every input as valid.
  â€¢ `cape-capf-case-fold', `cape-wrap-case-fold': Create a Capf which is
    case insensitive.
  â€¢ `cape-capf-debug', `cape-wrap-debug': Create a Capf which prints
    debugging messages.
  â€¢ `cape-capf-inside-code', `cape-wrap-inside-code': Ensure that Capf
    triggers only inside code.
  â€¢ `cape-capf-inside-comment', `cape-wrap-inside-comment': Ensure that
    Capf triggers only inside comments.
  â€¢ `cape-capf-inside-faces', `cape-wrap-inside-faces': Ensure that Capf
    triggers only inside text with certain faces.
  â€¢ `cape-capf-inside-string', `cape-wrap-inside-string': Ensure that
    Capf triggers only inside a string literal.
  â€¢ `cape-capf-interactive', `cape-interactive': Create a Capf which can
    be called interactively.
  â€¢ `cape-capf-nonexclusive', `cape-wrap-nonexclusive': Mark Capf as
    non-exclusive.
  â€¢ `cape-capf-noninterruptible', `cape-wrap-noninterruptible': Protect
    a Capf which does not like to be interrupted.
  â€¢ `cape-capf-passthrough', `cape-wrap-passthrough': Defeat entire
    completion style filtering.
  â€¢ `cape-capf-predicate', `cape-wrap-predicate': Add candidate
    predicate to a Capf.
  â€¢ `cape-capf-prefix-length', `cape-wrap-prefix-length': Enforce a
    minimal prefix length.
  â€¢ `cape-capf-properties', `cape-wrap-properties': Add completion
    properties to a Capf.
  â€¢ `cape-capf-purify', `cape-wrap-purify': Purify a broken Capf and
    ensure that it does not modify the buffer.
  â€¢ `cape-capf-silent', `cape-wrap-silent': Silence Capf messages and
    errors.
  â€¢ `cape-capf-sort', `cape-wrap-sort': Add sort function to a Capf.
  â€¢ `cape-capf-super', `cape-wrap-super': Merge multiple Capfs into a
    Super-Capf.

  In the following we show a few example configurations, which have come
  up on the [Cape] or [Corfu issue tracker] or the [Corfu wiki.] I use
  some of these tweaks in my personal configuration.

  â”Œâ”€â”€â”€â”€
  â”‚ ;; Example 1: Sanitize the `pcomplete-completions-at-point' Capf.  The Capf has
  â”‚ ;; undesired side effects on Emacs 28.  These advices are not needed on Emacs 29
  â”‚ ;; and newer.
  â”‚ (when (< emacs-major-version 29)
  â”‚   (advice-add 'pcomplete-completions-at-point :around #'cape-wrap-silent)
  â”‚   (advice-add 'pcomplete-completions-at-point :around #'cape-wrap-purify))
  â”‚ 
  â”‚ ;; Example 2: Configure a Capf with a specific auto completion prefix length
  â”‚ (setq-local completion-at-point-functions
  â”‚ 	    (list (cape-capf-prefix-length #'cape-dabbrev 2)))
  â”‚ 
  â”‚ ;; Example 3: Create a Capf with debugging messages
  â”‚ (setq-local completion-at-point-functions (list (cape-capf-debug #'cape-dict)))
  â”‚ 
  â”‚ ;; Example 4: Named Capf
  â”‚ (defalias 'cape-dabbrev-min-2 (cape-capf-prefix-length #'cape-dabbrev 2))
  â”‚ (setq-local completion-at-point-functions (list #'cape-dabbrev-min-2))
  â”‚ 
  â”‚ ;; Example 5: Define a defensive Dabbrev Capf, which accepts all inputs.  If you
  â”‚ ;; use Corfu and `corfu-auto=t', the first candidate won't be auto selected if
  â”‚ ;; `corfu-preselect=valid', such that it cannot be accidentally committed when
  â”‚ ;; pressing RET.
  â”‚ (defun my-cape-dabbrev-accept-all ()
  â”‚   (cape-wrap-accept-all #'cape-dabbrev))
  â”‚ (add-hook 'completion-at-point-functions #'my-cape-dabbrev-accept-all)
  â”‚ 
  â”‚ ;; Example 6: Define interactive Capf which can be bound to a key.  Here we wrap
  â”‚ ;; the `elisp-completion-at-point' such that we can complete Elisp code
  â”‚ ;; explicitly in arbitrary buffers.
  â”‚ (keymap-global-set "C-c p e" (cape-capf-interactive #'elisp-completion-at-point))
  â”‚ 
  â”‚ ;; Example 7: Ignore :keywords in Elisp completion.
  â”‚ (defun ignore-elisp-keywords (sym)
  â”‚   (not (keywordp sym)))
  â”‚ (setq-local completion-at-point-functions
  â”‚ 	    (list (cape-capf-predicate #'elisp-completion-at-point
  â”‚ 				       #'ignore-elisp-keywords)))
  â””â”€â”€â”€â”€


[Cape] <https://github.com/minad/cape/issues>

[Corfu issue tracker] <https://github.com/minad/corfu/issues>

[Corfu wiki.] <https://github.com/minad/corfu/wiki>


4 Contributions
â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•

  Since this package is part of [GNU ELPA] contributions require a
  copyright assignment to the FSF.


[GNU ELPA] <https://elpa.gnu.org/packages/cape.html>
